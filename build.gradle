import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'org.ajoberstar.reckon' version '0.9.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.github.ben-manes.versions' version '0.20.0'
}
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

group 'net.minecraftforge'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacoco {
    toolVersion = '0.8.2'
}

reckon {
    scopeFromProp()
    stageFromProp('milestone', 'rc', 'final')
}

reckonTagCreate.dependsOn check

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

test {
    useJUnitPlatform()
}

ext.antlr = [
        grammarpackage: 'net.minecraftforge.accesstransformer.generated',
        antlrSource: 'src/main/antlr4',
        destinationDir: "src/generated-sources/java"
]

configurations {
    antlr4 {
        description = 'ANTLR4'
    }
}

task antlrOutputDir {
    mkdir antlr.destinationDir
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec)

sourceSets {
    mlservice
    testJars
    antlr4 {
        java {
            srcDir {
                antlr.destinationDir
            }
        }
    }
}

compileJava {
    dependsOn generateGrammarSource
    source antlr.destinationDir
}

clean {
    delete antlr.destinationDir
}

ext.sharedManifest = manifest {
    attributes(['Specification-Title': 'accesstransformers',
                'Specification-Vendor': 'Forge',
                'Specification-Version': '1', // Currently version 1 of the accesstransformer specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${version}+${System.getenv("BUILD_NUMBER")?:0}+${grgit.head().abbreviatedId}",
                'Implementation-Vendor': 'Forge',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Git-Commit': grgit.head().abbreviatedId,
                'Git-Branch': grgit.branch.current().getName() ],
            'net/minecraftforge/accesstransformer/service/')
    attributes(['Specification-Title': 'accesstransformers',
                'Specification-Vendor': 'Forge',
                'Specification-Version': '1', // Currently version 1 of the accesstransformer specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${version}+${System.getenv("BUILD_NUMBER")?:0}+${grgit.head().abbreviatedId}",
                'Implementation-Vendor': 'Forge',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Git-Commit': grgit.head().abbreviatedId,
                'Git-Branch': grgit.branch.current().getName() ],
            'net/minecraftforge/accesstransformer/')
}

task mlserviceJar(type: Jar) {
    archiveClassifier = 'service'
    from sourceSets.main.output
    from sourceSets.mlservice.output
    manifest = project.manifest {
        from sharedManifest
    }
}

task testsJar(type: Jar) {
    archiveClassifier = 'testsjar'
    from sourceSets.testJars.output
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.mlservice.allSource
}


build.dependsOn testsJar
build.dependsOn mlserviceJar

repositories {
    mavenCentral()
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.+')
    testImplementation('org.powermock:powermock-core:2.0+')
    testImplementation('cpw.mods:modlauncher:0.10.+')
    testImplementation('com.google.code.gson:gson:2.8.5')
    testCompileOnly(sourceSets.mlservice.output)
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.4.+')
    testRuntime(sourceSets.testJars.runtimeClasspath)
    testRuntime(sourceSets.mlservice.output)
    implementation(sourceSets.antlr4.output)
    implementation('org.antlr:antlr4-runtime:4.7.2')
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4')
    implementation('org.ow2.asm:asm:6.2')
    implementation('org.ow2.asm:asm-commons:6.2')
    implementation('org.ow2.asm:asm-tree:6.2')
    implementation('org.apache.logging.log4j:log4j-api:2.11.+')
    implementation('org.apache.logging.log4j:log4j-core:2.11.+')
    implementation('cpw.mods:modlauncher:0.10.+:api')
    mlserviceImplementation('cpw.mods:modlauncher:0.10.+:api')
    mlserviceImplementation(sourceSets.main.output)
    antlr4Compile('org.antlr:antlr4:4.7.2')
}


// Shadow Jar with antlr embedded
shadowJar {
    classifier = 'shadowed'
    from sourceSets.main.output
    from sourceSets.mlservice.output
    manifest {
        inheritFrom mlserviceJar.manifest
        attributes('Main-Class': 'net.minecraftforge.accesstransformer.TransformerProcessor')
    }
    dependencies {
        exclude(dependency {
            it.moduleName != 'antlr4-runtime'
        })
    }
}

task fatJar(type: ShadowJar) {
    archiveClassifier = 'fatjar'
    from sourceSets.main.output
    from sourceSets.mlservice.output
    configurations = shadowJar.configurations
    manifest {
        inheritFrom mlserviceJar.manifest
        attributes('Main-Class': 'net.minecraftforge.accesstransformer.TransformerProcessor')
    }
    dependencies {
        exclude(dependency {
            it.moduleName == 'antlr4'
        })
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives testsJar
    archives mlserviceJar
    archives shadowJar
    archives fatJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact shadowJar
            artifact fatJar
            artifact mlserviceJar
            pom {
                name = 'Access Transformers'
                description = 'Transforms class member access based on specification files'
                url = 'https://github.com/MinecraftForge/AccessTransformers'
                scm {
                    url = 'https://github.com/MinecraftForge/AccessTransformers'
                    connection = 'scm:git:git://github.com/MinecraftForge/AccessTransformers.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/AccessTransformers.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/AccessTransformers/issues'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                    developer {
                        id = 'DemonWav'
                        name = 'DemonWav'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.forgeMavenUser?:'fake'
                password project.properties.forgeMavenPassword?:'news'
            }
            url 'http://files.minecraftforge.net/maven/manage/upload'
        }
    }
}

generateGrammarSource {
    description = 'Generates Java sources from ANTLR4 grammars.'
    inputs.dir file(antlr.antlrSource)
    outputs.dir file(antlr.destinationDir)
    def grammars = fileTree(antlr.antlrSource).include('**/*.g4')
    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4Compile
    def pkg = antlr.grammarpackage.replaceAll("\\.", "/")
    args = ["-o", "${antlr.destinationDir}/${pkg}", '-visitor', '-package', antlr.grammarpackage, grammars.files].flatten()
}
